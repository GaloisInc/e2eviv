-- This E2E VIV specification is written in the EBON specification
-- language, based upon the BON specification language from Walden and
-- Nerson.  See http://bon-method.com/ for more information about BON
-- and http://kindsoftware.com/ for more information about EBON.  Tool
-- support for EBON is provided by the BON compiler, bonc, and several
-- other tools.  See https://github.com/kiniry/BON for more
-- information.

-- Informal Charts

-- BON Domain Model

system_chart E2EVIV_SYSTEM
indexing
  author: "Joseph Kiniry <kiniry@galois.com>", "Daniel M. Zimmerman <dmz@galois.com>";
  organization: "Galois, Inc.";
  keywords: "OVF", "end-to-end verifiable internet voting", "e2e", "viv", 
    "verifiable elections", "software independence", 
    "high-assurance", "cryptography", "verification", "Galois";
  created: "Tue Jul 15 16:18:09 PDT 2014";
  revised: "February 2015";
  github: "https://github.com/GaloisInc/e2eviv";
explanation
  "An end-to-end verifiable Internet voting system"
cluster CORE_CONCEPTS
description
  "Core concepts generic to all elections."
cluster LEGAL_STANDARDS
description
  "Basic legal standards generic to democratic elections."
cluster PERSONAS
description
  "Archetypical personas for actors participating in elections."
end

cluster_chart CORE_CONCEPTS
explanation
  "Domain concepts core to the idea of elections."
cluster ACTIONS
description "State transitions that take place in the course of an election."
cluster BACKGROUND
description "Election background concepts."
cluster ELECTION_DAY_ARTIFACTS
description "Artifacts relevant to election day."
cluster PEOPLE
description "People that are part of an electoral system."
cluster PROPERTIES
description "Properties of elections and their artifacts."
cluster OTHERS
description "Placeholder for concepts waiting to be clustered."
end

cluster_chart BACKGROUND
explanation
  "Background concepts relevant to elections in the general."
class CONTEST
  description "A decision to be made, either the selection of one or  \
             \ more candidates for an office or the choice of a course of \
             \ action."
class DEMOCRACY 
  description "A system of government by an entire population or \
             \ eligible members thereof."
class ELECTION
  description "A formal process of selecting choices in one or more contests."
class ELECTORATE 
  description "The individuals that are eligible to vote in an election."
class EVOTING 
  description "A voting process carried out with the aid of electronic devices."
class INTERNET 
  description "A global system of interconnected computer networks."
class PARTY 
  description "An organized group of people with at least roughly similar \
             \ political aims."
class PRINCIPLE 
  description "A fundamental truth or proposition that serves as a foundation \
             \ for a system of belief or behavior or chain of reasoning."
class PROCEDURE 
  description "An established or official way of doing something."
class SYSTEM 
  description "A set of connected digital and physical parts, including computers, \
             \ people, organizations, and more, forming a complex whole."
class VOTING_CHANNEL 
  description "Methods by which voters are enabled to vote in elections."
cluster ELECTIONS 
  description "Types and attributes of elections."
cluster PROCESSES 
  description "A series of actions or steps taken in order to achieve a \
             \ particular end."
end

cluster_chart ELECTIONS
explanation
  "Types and attributes of elections."
class DIGITAL_ELECTION 
  description "An election carried out, in whole or in part, \
             \ by electronic means."
class INTERNET_ELECTION 
  description "An election carried out, in whole or in part, \
             \ using the Internet for the transmission of votes."
class SUPERVISED_ELECTION 
  description "An election where election officials, and perhaps the public, \
             \ watch over the election, primarily to ensure that voters have \
             \ privacy when voting."
class REMOTE_ELECTION 
  description "An election in which some or all of the ballots \
             \ are not cast at polling stations."
class TRADITIONAL_ELECTION 
  description "An election carried out entirely using physical \
             \ ballots cast at polling stations during \
             \ a fixed election period."
class UNSUPERVISED_ELECTION 
  description "An election where there are no observers ensuring voter \
             \ privacy."
class VERIFIABLE_ELECTION 
  description "An election scheme whereby one or more formal \
             \ properties of the election can be independently checked \
             \ by voters or election officials."
end

cluster_chart ELECTION_DAY_ARTIFACTS
explanation
  "Physical and digital artifacts relevant to election day."
class BALLOT 
  description "The legally recognized means by which a voter can \
             \ express his or her choices in one or more contests \
             \ in an election."
class BALLOT_BOX 
  description "The means by which ballots are stored pending being \
             \ counted."
class BALLOT_CONFIGURATION
  description "The set of contests in which voters of a particular \
             \ group are eligible to vote."
class BALLOT_QUESTION
  description "A decision among two or more courses of action."
class BALLOT_STYLE
  description "The concrete presentation of a ballot configuration."
class CANDIDATES_REGISTER 
  description "A listing of the candidates standing for election."
class CONTEST_CHOICE 
  description "A selection from among the possible outcomes of \
             \ a contest."
class CONTEST_FOR_OFFICE
  description "A selection of some subset of candidates standing for \
             \ election to a particular office."
class RESULT 
  description "The outcome of a contest."
class SEAL
  description "A device, algorithm, or process that provides evidence \
             \ for the integrity of an object."
class TALLY 
  description "The count of all the votes in an election."
class VOTE 
  description "The expression of a contest choice."
class VOTERS_REGISTER 
  description "A listing of the eligible voters for an election."
class VOTING_INTERFACE 
  description "The medium through which a voter makes his or \
             \ her contest choices known to the voting system."
cluster PHYSICAL_ARTIFACTS 
  description "Physical artifacts relevant to election day."
cluster DIGITAL_ARTIFACTS 
  description "Digital artifacts relevant to election day."
end

cluster_chart PHYSICAL_ARTIFACTS
explanation
  "Physical artifacts relevant to election day."
class DEVICE 
  description "A physical artifact that is fit for a particular purpose."
class PHYSICAL_BALLOT_BOX 
  description "A box used to store paper ballots pending being counted."
class PAPER_BALLOT 
  description "A ballot comprised of one or more pieces of paper."
class POLLING_STATION 
  description "A location at which physical ballot boxes and devices \
             \ are available for members of the electorate to use."
class PAPER_VOTE 
  description "A vote expressed with a paper ballot."
class PHYSICAL_SEAL 
  description "A piece of material that serves as evidence that \
             \ a physical artifact has not been tampered with."
end

cluster_chart DIGITAL_ARTIFACTS
explanation
  "Digital artifacts relevant to election day."
class DIGITAL_BALLOT 
  description "A digital manifestation of a paper ballot record."
class DIGITAL_BALLOT_BOX 
  description "The electronic means by which digital ballots are \
             \ stored pending being counted."
class DIGITAL_VOTE 
  description "A vote expressed with a digital ballot."
class DIGITAL_SEAL
  description "A digital artifact that serves as evidence that \
             \ another digital artifact has not been tampered with."
end

cluster_chart PROPERTIES
explanation
  "Properties of elections and related artifacts."
class ACCESSIBLE
  description "Usable by and useful to the disabled."
class AUTHENTICATED 
  description "A person or system that has properly identified itself."
class CONFIDENT 
  description "A person or system that is certain about a proposition."
class DIGITAL 
  description "An artifact that is manifested via information, rather than as \
             \ a tangible artifact."
class DISABLED 
  description "Having a physical or mental condition \
             \ that limits movements, senses, or activities."
class EFFICIENT 
  description "A system that operates in a productive fashion with \
             \ little wasted effort or expense."
class REMOTE 
  description "Located somewhere other than a polling station."
class ROBUST 
  description "A process or system that is able to withstand adverse \
             \ conditions."
class PHYSICAL 
  description "Manifested as a tangible artifact."
class SUPERVISED
  description "A person or system that is directly observed."
class TURNOUT
  description "The level of participation of voters in an election."
class UNSUPERVISED
  description "A person or system that has no observers."
class VERIFIABLE
  description "A system or algorithm that has properties which can be \
             \ checked by people or digital systems."
cluster ELECTION_PROPERTIES 
  description "Properties specific to elections."
cluster SYSTEM_PROPERTIES 
  description "Properties specific to systems used to \
             \ carry out elections."
end

cluster_chart PROCESSES
class AUDIT 
  description "A review of the election artifacts to detect \ 
             \ abnormalities and errors."
class CANDIDATE_REGISTRATION 
  description "The process by which a candidate is added to \
             \ the candidates register."
class CANDIDATE_VALIDATION 
  description "The process by which a candidate's identity and \
             \ eligibility to stand for election are verified."
class VOTER_REGISTRATION 
  description "The process by which a member of the electorate is added \
             \ to the voter register."
class VOTER_VALIDATION 
  description "The process by which a voter's identity and \
             \ eligibility to vote are verified."
class VERIFICATION 
  description "A process by which the the election protocols, \
             \ processes, and device implementations are proven \
             \ to be correct with respect to their specifications."
end

cluster_chart ACTIONS
explanation
  "State transitions that take place in the course of an election."
class AUTHENTICATE 
  description "Transitioning a person or system to an authenticated state."
class REGISTER_VOTER 
  description "The addition of a voter to the voters register."
class REGISTER_CANDIDATE 
  description "The addition of a candidate to the candidates register."
class UNREGISTER_VOTER
  description "The removal of a voter from the voters register."
class UNREGISTER_CANDIDATE
  description "The removal of a candidate from the candidates register."
end

cluster_chart ELECTION_PROPERTIES
explanation
  "Properties specific to elections."
class SECURITY
  description "The attestation that a system is free of certain classes of \
             \ dangers or threats."
cluster SCOPE 
  description "The geographic or logical area affected by an election."
end

cluster_chart SCOPE
explanation
  "The geographic or logical area affected by an election."
class INTERNATIONAL
  description "A scope comprised of multiple nations."
class LOCAL 
  description "A scope comprised of a single locality or \
             \ small group of localities."
class NATIONAL 
  description "A scope comprised of a single nation."
class REGIONAL
  description "A scope comprised of a large group of localities \
             \ or a single state, territory, or similar region"
end

cluster_chart SYSTEM_PROPERTIES
explanation
  "Properties held by election systems, digital or physical."
class ACCESSIBLE
  description "An artifact is accessible if it is usable and useful to \
             \ the disabled."
class RELIABLE 
  description "An artifact is reliable if it is guaranteed to be \
             \ available for use, and to work properly, for a \
             \ specified percentage of time."
class USABLE 
  description "An artifact is usable if it can successfully be \
             \ used for its stated purpose."
end

cluster_chart PEOPLE
explanation
  "The people that are part of an electoral system.  These are the most \
 \ general concepts explaining roles in an election from a legal standpoint. \
 \ Personas, as specified in the PERSONAS cluster, further concretize these \
 \ general classes into specific UX-centric roles."
class CANDIDATE 
  description "A citizen who is standing for election."
class CITIZEN 
  description "A person who legally belongs to a place and \ 
             \ has the rights and protections of that place."
class ELECTORAL_AUTHORITY 
  description "An individual or group responsible for setting up, \
             \ running, and disclosing the results of an election."
class IDENTITY 
  description "The fact of being who or what a person or system is."
class PERSON
  description "An individual."
class VOTER 
  description "A person who is eligible to vote in an election."
end

cluster_chart LEGAL_STANDARDS
explanation
  "Basic legal standards generic to democratic elections."
class LEGISLATION
  description "A set of laws."
end

class_chart ACCESSIBLE
indexing
  applicable_law: "http://www.section508.gov"
explanation
  "An artifact is accessible if it is usable by and useful to the \
 \ disabled.  The details of such a notion are often codified in \
 \ federal law (e.g., U.S. Section 508) and are the results of \
 \ researchers focused on user experiences for the disabled."
end

class_chart AUDIT
explanation
  "An audit is a review of the election artifacts to detect \ 
 \ abnormalities and errors."
query
  "Were any errors detected?"
end

class_chart AUTHENTICATE
explanation
  "A person or system that has properly identified itself."
end

class_chart BALLOT
explanation
  "A ballot is the legally recognized means by which a voter \
 \ can express his or her choices for one or more contests in an \
 \ election. A ballot contains a set of votes corresponding to \
 \ the contests being decided in the election."
query
  "What is your ballot style?",
  "Have you been cast?",
  "Have you been spoiled?", 
  "What votes do you contain?",
  "What vote do you contain for the_contest?"
command
  "Your ballot style is the_style!",
  "You are cast!",
  "You are spoiled!",
  "You contain the_vote for the_contest!",
  "You no longer contain a vote for the_contest!"
constraint
  "Initially a ballot is not cast, not spoiled, and contains no votes.",
  "A ballot's style may only be set once.",
  "Once a ballot is cast or spoiled, the set of votes it contains \
 \ cannot be changed.",
  "Once a ballot is cast, it remains cast forever.",
  "Once a ballot is spoiled, it remains spoiled forever.",
  "A ballot cannot be both cast and spoiled.",
  "A ballot may contain at most one vote per contest.",
  "A ballot may only contain votes for contests that are part of its \
 \ ballot style."
end

class_chart BALLOT_BOX
explanation
  "A ballot box is the means by which ballots are stored pending \
 \ being counted. It may take various forms, including physical \
 \ containers (from which the name derives) and electronic \
 \ storage media."
query
  "Are you empty?",
  "Are you full?",
  "What is your capacity?",
  "How many ballots do you contain?",
  "What ballots do you contain?",
  "What seals are applied to you?",
  "Are you sealed?"
command
  "You contain the_ballot!",
  "You no longer contain the_ballot!",
  "The_seal is applied to you!",
constraint
  "A ballot box is initially empty and has no seals.",
  "Only cast ballots may be added to a ballot box.",
  "Ballots that are not in a ballot box cannot be \
 \ removed from the ballot box.",
  "A ballot box is sealed if and only if it has one or \
 \ more unbroken seals applied to it.",
  "Ballots may not be added to a full or sealed ballot box.",
  "Ballots may not be removed from an empty or sealed ballot box.",
  "A ballot box's capacity is non-negative.",
  "A ballot box is full if and only if it contains a number of \
 \ ballots equal to its capacity."
end

class_chart BALLOT_CONFIGURATION
explanation
  "A ballot configuration is the set of contests in which voters \
 \ of a particular group are eligible to vote."
query
  "What contests do you contain?",
  "Are you locked?"
command
  "You contain the_contest!",
  "You do not contain the_contest!",
  "You are locked!"
constraint
  "A ballot configuration is initially unlocked and contains \
 \ no contests.",
  "A contest may not be added to a ballot configuration more \
 \ than once.",
  "Ballot configurations that contain no contests may not be \
 \ locked.",
  "A contest may not be added to or removed from a locked \
 \ ballot configuration.",
  "Once locked, a ballot configuration remains locked forever." 
end

class_chart BALLOT_QUESTION
explanation
  "A ballot question is a decision among two or more courses of \
 \ action. In most cases, ballot questions take the form of yes/no \
 \ questions."
inherit CONTEST
constraint
  "The maximum number of choices that may be chosen on the same \
 \ ballot is 1.",
  "Each choice represents a course of action." 
end

class_chart BALLOT_STYLE
explanation
  "A ballot style is the concrete presentation of a ballot \
 \ configuration. A single ballot configuration may be realized \
 \ by several ballot styles, e.g. for different languages or \
 \ different orderings of contests or contest choices."
query
  "What is your ballot configuration?",
  "What are the details of your presentation?" -- highly implementation dependent
command
  "Your ballot configuration is the_configuration!"
constraint
  "A ballot style's ballot configuration may only be set once.",
  "A ballot style's ballot configuration may only be set to a \
 \ locked ballot configuration."
end
 
class_chart CANDIDATE
explanation
  "A candidate is a citizen who is listed on the ballot as a \
 \ possible choice to fill some position."
inherit CITIZEN
-- constraint
 -- various constraints apply to candidates for various offices,
 -- depending on the office (e.g., >= 35 yrs old, permanent
 -- resident in the U.S. for >= 14 yrs, and born a U.S.
 -- citizen are constraints on candidates for President of the
 -- United States).
end

class_chart CANDIDATES_REGISTER
explanation
  "A candidates' register contains all the candidates standing \
 \ for election and the offices for which they are standing."
query
  "Do you contain the_candidate?",
  "What candidates do you contain?",
  "Are you locked?"
command
  "You contain the_candidate!",
  "You do not contain the_candidate!",
  "You are locked!"
constraint
  "A register is initially empty and unlocked.",
  "A candidate may not be added to the register more than once.",
  "A candidate who is not in the register cannot be removed from \
 \ the register.", 
  "Candidates may not be added to or removed from a locked register.",
  "A locked register may not be unlocked.",
  "A candidate added to the register must be alive at the time of \
 \ addition."
end

class_chart CITIZEN
inherit PERSON
explanation
  "A citizen is a person who legally belongs to a place and \ 
 \ has the rights and protections of that place. The place can \
 \ be at any election scope; for example, a particular person \
 \ may be a citizen of the United States, of Oregon, and of \
 \ Portland simultaneously."
query
  "What is your current residence address?",
  "What is your citizenship information?"
command
  "Your current residence address is the_address!",
  "Your citizenship information is the_information!"
end

class_chart CONFIDENT
explanation
  "A person or system that is certain about a proposition."
end

class_chart CONTEST
explanation
  "A contest is a decision to be made, either the selection of one or  \
 \ more candidates for an office or the choice of a course of action. \ 
 \ There may be multiple contests in a single election."
query
  "What are your contest choices?",
  "Do you allow a write-in choice?",
  "What is the maximum number of your contest choices that can be chosen \
 \ on the same ballot?",
  "What is your result?",
  "Does the_vote contain valid choices for you?"
command
  "Your contest choices are the_choices!",
  "You do/do not allow a write-in choice!",
  "Your result is the_result!",
  "The_maximum_number of your contest choices can be chosen on the \
 \ same ballot!"
constraint
  "A contest must have at least one contest choice.",
  "The result must contain information about every contest choice \
 \ in the contest.",
  "The contest choices for a contest may only be set once.",
  "The write-in allowed flag may only be set once.",
  "The result for a contest may only be set once.",
  "The maximum number of contest choices that can be chosen on the same \
 \ ballot may only be set once."
end

class_chart CONTEST_CHOICE
explanation
  "A selection from among the possible outcomes of a contest."
end

class_chart CONTEST_FOR_OFFICE
explanation
  "A contest for office is a selection of some subset of candidates \
 \ standing for election to a particular office."
inherit CONTEST
constraint
  "The contest choices represent individual candidates for an office."
end

class_chart DEMOCRACY
explanation
  "Democracy is a system of government (of a nation, \
 \ state, locality, or other organization) by an entire \
 \ population or eligible members thereof, typically \
 \ carried out through elections of leaders and \
 \ representatives, through referenda, or both."
end

class_chart DEVICE
explanation
  "A physical artifact that is fit for a particular purpose."
end

class_chart DIGITAL_BALLOT
explanation
  "A digital ballot is a ballot comprised of one or more \
 \ digital artifacts."
inherit BALLOT
end

class_chart DIGITAL_BALLOT_BOX
explanation
  "A digital ballot box is the electronic means by which \
 \ digital ballots are stored pending being counted."
inherit BALLOT_BOX
end

class_chart DIGITAL_SEAL
explanation
  "A digital seal is a piece of evidence that a digital \
 \ artifact has not been tampered with. Such seals \
 \ typically take the form of cryptographic hashes."
inherit SEAL
end

class_chart DISABLED
explanation
  "An individual is disabled if he or she has a physical or \
 \ mental condition that limits movements, senses, or \
 \ activities in a way that affects his or her ability to \
 \ participate in an election."
end

class_chart EFFICIENT
explanation
  "A system that operates in a productive fashion with little wasted \
 \ effort or expense."
end

class_chart ELECTION
explanation
  "An election is a formal indication of choices \
 \ in one or more contests."
end

class_chart ELECTORAL_AUTHORITY
explanation
  "An electoral authority is an individual or group responsible \
 \ for setting up, running, and disclosing the results of an election."
query
  "Who are your members?"
command
  "The_citizen is one of your members!",
  "The_citizen is not one of your members!"
constraint
  "An electoral authority must have at least one member."
end

class_chart ELECTORATE
explanation
  "The electorate for a given election comprises all the \
 \ individuals that are eligible to vote in that election, \
 \ according to applicable laws and regulations."
end

class_chart EVOTING
explanation
  "E-Voting, or electronic voting, is the process of voting either \
 \ entirely using, or with substantial assistance from, electronic \
 \ devices."
end

class_chart IDENTITY
explanation
  "The fact of being who or what a person or system is."
end

class_chart INTERNATIONAL
explanation
  "An international scope is comprised of multiple nations; \
 \ for example, the European Union."
end

class_chart INTERNET
explanation
  "The Internet is a global system of interconnected computer \
 \ networks that use the standard Internet Protocol suite to \
 \ link billions of connected devices."
end

class_chart LEGISLATION
explanation
  "Legislation is a set of laws. In this context, it is \
 \ the set of laws dealing with the conduct of elections."
end

class_chart LOCAL
explanation
  "A local scope is comprised of a single locality or a small \
 \ group of localities. Examples include individual towns and counties."
end

class_chart NATIONAL
explanation
  "A national scope is comprised of a single nation."
end

class_chart PAPER_BALLOT
explanation
  "A paper ballot is a ballot implemented using paper, which \
 \ a voter marks in some fashion to indicate his or her choice."
inherit BALLOT
end

class_chart PAPER_VOTE
explanation
  "A paper vote is a vote expressed with a paper ballot."
inherit VOTE
-- queries, commands, constraints to do with legibility and
-- interpretation of voter intent? 
end

class_chart PERSON
explanation
  "A person is an individual with a unique identity."
query
  "What is your name?",
  "What is your date of birth?", 
  "Are you alive?"
command
  "Your name is the_name!",
  "Your date of birth is the_date!",
  "You are not alive!"
constraint
  "A person is initially alive.",
  "A person's date of birth may only be set once.",
  "A person's date of birth must be in the past.",
  "Once a person is not alive, he stays not alive forever."
end

class_chart PARTY
explanation
  "A party is an organized group of people having at least roughly \
 \ similar political aims and opinions, that seeks to influence \
 \ public policy by getting its candidates elected to public office."
end

class_chart PHYSICAL_BALLOT_BOX
explanation
  "A physical ballot box is a box used to store paper ballots \
 \ pending being counted."
inherit BALLOT_BOX
query
  "What is your chain of custody?"
command
  "Add the_entry to your chain of custody!"
constraint
  "The capacity of a physical ballot box is finite.",
  "A physical ballot box may only be sealed with physical seals.",
  "A physical ballot box may only contain paper ballots.",
  "Entries may not be removed from the chain of custody.",
  "Each entry added to the chain of custody must represent one or \
 \ more individuals."
end

class_chart PHYSICAL_SEAL
explanation
  "A physical seal is a piece of material (such as tape) that \
 \ serves as evidence that a physical artifact has not been \
 \ tampered with. Such a seal typically has a distinctive design \
 \ and other features that make it obvious when a sealed artifact \
 \ has been tampered with."
inherit SEAL
query
  "What is your identifier?"
command
  "Your identifier is the_identifier!"
constraint
  "The identifier of a physical seal may only be set once."
end

class_chart POLLING_STATION
explanation
  "A polling station is a location at which physical ballot \
 \ boxes and devices are available for members of the \
 \ electorate to use during an election. Polling stations \
 \ are typically staffed by poll workers and have well-publicized \
 \ hours of operation."
end

class_chart PRINCIPLE
explanation
  "A principle is a fundamental truth or proposition that serves \
 \ as a foundation for a system of belief or behavior or chain \
 \ of reasoning."
end

class_chart PROCEDURE
explanation
  "A procedure is an established or official way of doing something."
end

class_chart REGIONAL
explanation
  "A regional scope is comprised of a large group of localities \
 \ or a single state, territory, or similar region. For example, \
 \ the state of California."
end

class_chart RELIABLE
explanation
  "An artifact is reliable if it is guaranteed to be \
 \ available for use, and to work properly, for a \
 \ specified percentage of time."
query
  "What is your reliability percentage?"
constraint
  "The reliability percentage must be between 0 and 100, \
 \ inclusive"
end

class_chart REMOTE
explanation
  "Something is remote if it is located somewhere other \
 \ than a polling station."
end

class_chart RESULT
explanation
  "A result is the outcome of a contest."
query
  "What contest choices do you contain counts for?",
  "What is the count for the_choice?"
command
  "The count for the_choice is the_number_of_votes!"
constraint
  "The count for each contest choice may only be set once."
end

class_chart ROBUST
explanation
  "A process or system is robust if it is able to \
 \ withstand adverse conditions."
end

class_chart SEAL
explanation 
  "A seal provides some evidence of the integrity of another \
 \ entity (such as a ballot or ballot box). Its form depends on \
 \ the form of the entity being sealed, as well as other system \
 \ characteristics."
query
  "Are you applied?",
  "What are you applied to?",
  "Are you broken?"
command
  "You are applied to the_entity!",
  "Break!"
constraint
  "A seal is initially not applied and not broken.",
  "A seal may only be applied if it has not previously been applied \
 \ or broken.",
  "A seal may only be broken if it has not previously been broken.",
  "Once applied, a seal remains applied forever.",
  "Once broken, a seal remains broken forever."
end

class_chart SECURITY
explanation
  "Security is the attestation that a system is free of certain \
 \ classes of dangers or threats."
end

class_chart SYSTEM
explanation
  "A system is a set of connected digital and physical parts, \
 \ including computers, people, organizations, and more, \
 \ forming a complex whole."
end

class_chart TALLY
explanation
  "The tally is the count of all the votes in an election."
end

class_chart TURNOUT
explanation
  "Turnout is the level of participation of voters in a contest."
end

class_chart USABLE
explanation
  "An artifact is usable if it can successfully be used for its stated \
 \ purpose."
end

class_chart VERIFIABLE
explanation
  "A system or algorithm is verifiable if it has properties \
 \ that can be checked by people or digital systems."
end

class_chart VERIFICATION
explanation
  "Verification is a process by which the the election \ 
 \ protocols, processes, and device implementations are proven \ 
 \ to be correct with respect to their specifications."
end

class_chart VOTE
explanation
  "A vote is an expression of a contest choice made by a voter \
 \ during an election."
query
  "What contest choice do you represent?"
command
  "You represent the_choice!"
constraint
  "The choice represented by a vote may only be set once."
end

class_chart VOTER
explanation
  "A voter is a person who is eligible to vote in an election. \
 \ A voter has affiliations with various groups (districts, \
 \ political parties, etc.) that determine the voter's \
 \ eligibility to vote in specific contests."
inherit CITIZEN
query
  "What groups are you in?"
command
  "You are in the_group!",
  "You are not in the_group!"
end

class_chart VOTER_REGISTRATION
explanation
  "Voter registration is the process by which a member of \
 \ the electorate is added to the voters' register."
end

class_chart VOTERS_REGISTER
explanation
  "A voters' register contains all the voters eligible \
 \ to vote in an election, as well as information \
 \ indicating the ballot configuration each voter should \
 \ use and whether each has participated in the election."
query
  "Is the_voter in the register?",
  "What ballot configuration should be used with the_voter?",
  "Has the_voter voted?"
command
  "Add the_voter to the register!",
  "Remove the_voter from the register!",
  "Set the_voter's ballot configuration to the_configuration!",
  "the_voter has voted!"
constraint
  "A voter may not be added to the register more than once.",
  "A voter may not be removed from the register if he has voted.",
  "Once a voter is marked as having voted, he remains so marked forever.",
  "Once a voter is marked as having voted, his ballot configuration may \
 \ not change."
end

class_chart VOTING_CHANNEL
explanation
  "A voting channel is a method by which voters are enabled \
 \ to vote in elections."
end

class_chart VOTING_INTERFACE
explanation
  "A voting interface is the medium through which a voter \ 
 \ makes his or her contest choices known to the voting system."
end
