-- This E2E VIV specification is written in the EBON specification
-- language, based upon the BON specification language from Walden and
-- Nerson.  See http://bon-method.com/ for more information about BON
-- and http://kindsoftware.com/ for more information about EBON.  Tool
-- support for EBON is provided by the BON compiler, bonc, and several
-- other tools.  See https://github.com/kiniry/BON for more
-- information.

-- Informal Charts

-- BON Domain Model

system_chart E2EVIV_SYSTEM
indexing
  author: "Joseph Kiniry <kiniry@galois.com>", "Daniel M. Zimmerman <dmz@galois.com>";
  organization: "Galois, Inc.";
  keywords: "OVF", "end-to-end verifiable internet voting", "e2e", "viv", 
    "verifiable elections", "software independence", 
    "high-assurance", "cryptography", "verification", "Galois";
  created: "Tue Jul 15 16:18:09 PDT 2014";
  revised: "October 2014";
  github: "https://github.com/GaloisInc/e2eviv";
explanation
  "An end-to-end verifiable Internet voting system"
cluster CORE_CONCEPTS
description
  "Core concepts generic to all elections."
cluster LEGAL_STANDARDS
description
  "Basic legal standards generic to democratic elections."
cluster PERSONAS
description
  "Archetypical personas for actors participating in elections."
end

cluster_chart CORE_CONCEPTS
explanation
  "Domain concepts core to the idea of elections."
cluster ACTIONS
description "State transitions that take place in the course of an election."
cluster BACKGROUND
description "Election background concepts."
cluster ELECTION_DAY_ARTIFACTS
description "Artifacts relevant to election day."
cluster PEOPLE
description "People that are part of an electoral system."
cluster PROPERTIES
description "Properties of elections and their artifacts."
cluster OTHERS
description "Placeholder for concepts waiting to be clustered."
end

cluster_chart BACKGROUND
explanation
  "Background concepts relevant to elections in the general."
class DEMOCRACY 
  description "A system of government by an entire population or \
             \ eligible members thereof."
class ELECTION
  description "A formal indication of a choice between two or more \
             \ candidates or courses of action."
class ELECTORATE 
  description "The individuals that are eligible to vote in an election."
class EVOTING 
  description "A voting process carried out with the aid of electronic devices."
class INTERNET 
  description "A global system of interconnected computer networks."
class PARTY 
  description "An organized group of people with at least roughly similar \
             \ political aims."
class PRINCIPLE 
  description "A fundamental truth or proposition that serves as a foundation \
             \ for a system of belief or behavior or chain of reasoning."
class PROCEDURE 
  description "An established or official way of doing something."
class SYSTEM 
  description "A set of connected digital and physical parts, including computers, \
             \ people, organizations, and more, forming a complex whole."
class VOTING_CHANNEL 
  description "Methods by which voters are enabled to vote in elections."
cluster ELECTIONS 
  description "Types and attributes of elections."
cluster PROCESSES 
  description "A series of actions or stpes taken in order to achieve a \
             \ particular end."
end

cluster_chart ELECTIONS
explanation
  "Types and attributes of elections."
class DIGITAL_ELECTION 
  description "An election carried out, in whole or in part, \
             \ by electronic means."
class INTERNET_ELECTION 
  description "An election carried out, in whole or in part, \
             \ using the Internet for the transmission of votes."
class SUPERVISED_ELECTION 
  description "An election where election officials, and perhaps the public, \
             \ watch over the election, primarily to ensure that voters have \
             \ privacy when voting."
class REMOTE_ELECTION 
  description "An election in which some or all of the ballots \
             \ are not cast at polling stations."
class TRADITIONAL_ELECTION 
  description "An election carried out entirely using physical \
             \ ballots cast at polling stations during \
             \ a fixed election period."
class UNSUPERVISED_ELECTION 
  description "An election where there are no observers ensuring voter \
             \ privacy."
class VERIFIABLE_ELECTION 
  description "An election scheme whereby one or more formal \
             \ properties of the election can be independently checked \
             \ by voters or election officials."
end

cluster_chart ELECTION_DAY_ARTIFACTS
explanation
  "Physical and digital artifacts relevant to election day."
class BALLOT 
  description "The legally recognized means by which a voter can \
             \ express his or her choice of voting option."
class BALLOT_BOX 
  description "The means by which votes are stored pending being \
             \ counted."
class CANDIDATES_REGISTER 
  description "A listing of the candidates standing for election."
class REFERENDUM 
  description "A decision among two or more courses of action."
class RESULT 
  description "The outcome of an election."
class SEAL
  description "A device, algorithm, or process that provides evidence \
             \ for the integrity of an object."
class TALLY 
  description "The count of all the votes in an election."
class VOTE 
  description "The expression of the choice of voting option."
class VOTERS_REGISTER 
  description "A listing of the eligible voters for an election."
class VOTING_INTERFACE 
  description "The medium through which a voter makes his or \
             \ her choices of voting option known to the voting \
             \ system."
class VOTING_OPTION 
  description "A selection from among the possible outcomes of \
             \ an election."
cluster PHYSICAL_ARTIFACTS 
  description "Physical artifacts relevant to election day."
cluster DIGITAL_ARTIFACTS 
  description "Digital artifacts relevant to election day."
end

cluster_chart PHYSICAL_ARTIFACTS
explanation
  "Physical artifacts relevant to election day."
class DEVICE 
  description "A physical artifact that is fit for a particular purpose."
class PHYSICAL_BALLOT_BOX 
  description "A box used to store paper votes pending being counted."
class PAPER_BALLOT 
  description "A ballot comprised of one or more pieces of paper."
class POLLING_STATION 
  description "A location at which physical ballot boxes and devices \
             \ are available for members of the electorate to use."
class PAPER_VOTE 
  description "A vote expressed with a paper ballot."
class PHYSICAL_SEAL 
  description "A piece of material that serves as evidence that \
             \ a physical artifact has not been tampered with."
end

cluster_chart DIGITAL_ARTIFACTS
explanation
  "Digital artifacts relevant to election day."
class DIGITAL_BALLOT 
  description "A digital manifestation of a paper ballot record."
class DIGITAL_BALLOT_BOX 
  description "The electronic means by which digital votes are \
             \ stored pending being counted."
class DIGITAL_VOTE 
  description "A vote expressed with a digital ballot."
class DIGITAL_SEAL
  description "A digital artifact that serves as evidence that \
             \ another digital artifact has not been tampered with."
end

cluster_chart PROPERTIES
explanation
  "Properties of elections and related artifacts."
class ACCESSIBLE
  description "Usable by and useful to the disabled."
class AUTHENTICATED 
  description "A person or system that has properly identified itself."
class CONFIDENT 
  description "A person or system that is certain about a proposition."
class DIGITAL 
  description "An artifact that is manifested via information, rather than as \
             \ a tangible artifact."
class DISABLED 
  description "Having a physical or mental condition \
             \ that limits movements, senses, or activities."
class EFFICIENT 
  description "A system that operates in a productive fashion with \
             \ little wasted effort or expense."
class REMOTE 
  description "Located somewhere other than a polling station."
class ROBUST 
  description "A process or system that is able to withstand adverse \
             \ conditions."
class PHYSICAL 
  description "Manifested as a tangible artifact."
class SUPERVISED
  description "A person or system that is directly observed."
class TURNOUT
  description "The level of participation of voters in an election."
class UNSUPERVISED
  description "A person or system that has no observers."
class VERIFIABLE
  description "A system or algorithm that has properties which can be \
             \ checked by people or digital systems."
cluster ELECTION_PROPERTIES 
  description "Properties specific to elections."
cluster SYSTEM_PROPERTIES 
  description "Properties specific to systems used to \
             \ carry out elections."
end

cluster_chart PROCESSES
class AUDIT 
  description "A review of the election artifacts to detect \ 
             \ abnormalities and errors."
class CANDIDATE_REGISTRATION 
  description "The process by which a candidate is added to \
             \ the candidates register."
class CANDIDATE_VALIDATION 
  description "The process by which a candidate's identity and \
             \ eligibility to stand for election are verified."
class VOTER_REGISTRATION 
  description "The process by which a member of the electorate is added \
             \ to the voter register."
class VOTER_VALIDATION 
  description "The process by which a voter's identity and \
             \ eligibility to vote are verified."
class VERIFICATION 
  description "A process by which the the election protocols, \
             \ processes, and device implementations are proven \
             \ to be correct with respect to their specifications."
end

cluster_chart ACTIONS
explanation
  "State transitions that take place in the course of an election."
class AUTHENTICATE 
  description "Transitioning a person or system to an authenticated state."
class REGISTER_VOTER 
  description "The addition of a voter to the voters register."
class REGISTER_CANDIDATE 
  description "The addition of a candidate to the candidates register."
class UNREGISTER_VOTER
  description "The removal of a voter from the voters register."
class UNREGISTER_CANDIDATE
  description "The removal of a candidate from the candidates register."
end

cluster_chart ELECTION_PROPERTIES
explanation
  "Properties specific to elections."
class SECURITY
  description "The attestation that a system is free of certain classes of \
             \ dangers or threats."
cluster SCOPE 
  description "The geographic or logical area affected by an election."
end

cluster_chart SCOPE
explanation
  "The geographic or logical area affected by an election."
class INTERNATIONAL
  description "A scope comprised of multiple nations."
class LOCAL 
  description "A scope comprised of a single locality or \
             \ small group of localities."
class NATIONAL 
  description "A scope comprised of a single nation."
class REGIONAL
  description "A scope comprised of a large group of localities \
             \ or a single state, territory, or similar region"
end

cluster_chart SYSTEM_PROPERTIES
explanation
  "Properties held by election systems, digital or physical."
class ACCESSIBLE
  description "An artifact is accessible if it is usable and useful to \
             \ the disabled."
class RELIABLE 
  description "An artifact is reliable if it is guaranteed to be \
             \ available for use, and to work properly, for a \
             \ specified percentage of time."
class USABLE 
  description "An artifact is usable if it can successfully be \
             \ used for its stated purpose."
end

cluster_chart PEOPLE
explanation
  "The people that are part of an electoral system.  These are the most \
 \ general concepts explaining roles in an election from a legal standpoint. \
 \ Personas, as specified in the PERSONAS cluster, further concretize these \
 \ general classes into specific UX-centric roles."
class CANDIDATE 
  description "A citizen who is standing for election."
class CITIZEN 
  description "A person who legally belongs to a place and \ 
             \ has the rights and protections of that place."
class ELECTORAL_AUTHORITY 
  description "An individual or group responsible for setting up, \
 \ running, and disclosing the results of an election."
class IDENTITY 
  description "The fact of being who or what a person or system is."
class VOTER 
  description "A person who is eligible to vote in an election."
end

cluster_chart LEGAL_STANDARDS
explanation
  "Basic legal standards generic to democratic elections."
class LEGISLATION
  description "A set of laws."
end

class_chart ACCESSIBLE
indexing
  applicable_law: "http://www.section508.gov"
explanation
  "An artifact is accessible if it is usable by and useful to the \
 \ disabled.  The details of such a notion are often codified in \
 \ federal law (e.g., U.S. Section 508) and are the results of \
 \ researchers focused on user experiences for the disabled."
end

class_chart AUDIT
explanation
  "An audit is a review of the election artifacts to detect \ 
 \ abnormalities and errors."
query
  "Were any errors detected?"
end

class_chart AUTHENTICATE
explanation
  "A person or system that has properly identified itself."
end

class_chart BALLOT
explanation
  "A ballot is the legally recognized means by which a voter \
 \ can express his or her choice of voting option. A ballot \ 
 \ contains a set of possible options, either candidates for \
 \ a position or courses of action to be taken."
query
  "Have you been cast?",
  "Have you been spoiled?", 
  "What choices do you represent?"
command
  "You are cast!",
  "You are spoiled!",
  "You represent the_choice!"
constraint
  "Initially, a ballot is not cast, not spoiled, and represents no choices.",
  "Once a ballot is cast or spoiled, its choices cannot be changed.",
  "Once a ballot is cast, it remains cast forever.",
  "Once a ballot is spoiled, it remains spoiled forever.",
  "A ballot cannot be both cast and spoiled."
end

class_chart PAPER_BALLOT
explanation
  "A paper ballot is a ballot implemented using paper, which \
 \ the voter marks in some fashion to indicate his or her choice."
inherit BALLOT
end

class_chart BALLOT_BOX
explanation
  "A ballot box is the means by which votes are stored pending \
 \ being counted. It may take various forms, including physical \
 \ containers (from which the name derives) and electronic \
 \ storage media."
query
  "Are you full?",
  "What is your capacity?",
  "How many ballots do you contain?",
  "What ballots do you contain?"
command
  "You contain the_ballot!",
  "You no longer contain the_ballot!"
constraint
  "Only cast ballots may be added to the ballot box.",
  "Ballots that are not in the ballot box cannot be \
 \ removed from the ballot box.",
  "Ballots may not be added to a full ballot box.",
  "A ballot box is full if it contains a number of ballots \
 \ equal to its capacity."
end

class_chart CANDIDATE
explanation
  "A candidate is a citizen who is listed on the ballot as a \
 \ possible choice to fill some position."
end

class_chart CITIZEN
explanation
  "A citizen is a person who legally belongs to a place and \ 
 \ has the rights and protections of that place. The place can \
 \ be at any election scope; for example, a particular person \
 \ may be a citizen of the United States, of Oregon, and of \
 \ Portland simultaneously."
end

class_chart CANDIDATES_REGISTER
explanation
  "A candidates' register contains all the candidates \
 \ standing for election."
query
  "Is the_candidate in the register?"
command
  "Add the_candidate to the register!",
  "Remove the_candidate from the register!"
constraint
  "A candidate may not be added to the register more than once."
end

class_chart CONFIDENT
explanation
  "A person or system that is certain about a proposition."
end

class_chart DEMOCRACY
explanation
  "Democracy is a system of government (of a nation, \
 \ state, locality, or other organization) by an entire \
 \ population or eligible members thereof, typically \
 \ carried out through elections of leaders and \
 \ representatives, through referenda, or both."
end

class_chart DEVICE
explanation
  "A physical artifact that is fit for a particular purpose."
end

class_chart DIGITAL_BALLOT
explanation
  "A digital ballot is a ballot comprised of one or more \
 \ digital artifacts."
inherit BALLOT
end

class_chart DIGITAL_BALLOT_BOX
explanation
  "A digital ballot box is the electronic means by which \
 \ digital votes are stored pending being counted."
inherit BALLOT_BOX
end

class_chart DIGITAL_SEAL
explanation
  "A digital seal is a piece of evidence that a digital \
 \ artifact has not been tampered with. Such seals \
 \ typically take the form of cryptographic hashes."
inherit SEAL
end

class_chart DISABLED
explanation
  "An individual is disabled if he or she has a physical or \
 \ mental condition that limits movements, senses, or \
 \ activities in a way that affects his or her ability to \
 \ participate in an election."
end

class_chart EFFICIENT
explanation
  "A system that operates in a productive fashion with little wasted \
 \ effort or expense."
end

class_chart ELECTION
explanation
  "An election is a formal indication of a choice \
 \ between two or more candidates or courses of action."
end

class_chart ELECTORAL_AUTHORITY
explanation
  "An electoral authority is an individual or group responsible \
 \ for setting up, running, and disclosing the results of an election."
end

class_chart ELECTORATE
explanation
  "The electorate for a given election comprises all the \
 \ individuals that are eligible to vote in that election, \
 \ according to applicable laws and regulations."
end

class_chart EVOTING
explanation
  "E-Voting, or electronic voting, is the process of voting either \
 \ entirely using, or with substantial assistance from, electronic \
 \ devices."
end

class_chart IDENTITY
explanation
  "The fact of being who or what a person or system is."
end

class_chart INTERNATIONAL
explanation
  "An international scope is comprised of multiple nations; \
 \ for example, the European Union."
end

class_chart INTERNET
explanation
  "The Internet is a global system of interconnected computer \
 \ networks that use the standard Internet Protocol suite to \
 \ link billions of connected devices."
end

class_chart LEGISLATION
explanation
  "A set of laws."
end

class_chart LOCAL
explanation
  "A local scope is comprised of a single locality or a small \
 \ group of localities. Examples include individual towns and counties."
end

class_chart NATIONAL
explanation
  "A national scope is comprised of a single nation."
end

class_chart PARTY
explanation
  "An organized group of people with at least roughly similar \
 \ political aims and opinions, that seeks to influence public \
 \ policy by getting its candidates elected to public office."
end

class_chart PHYSICAL_SEAL
explanation
  "A physical seal is a piece of material (such as tape) that \
 \ serves as evidence that a physical artifact has not been \
 \ tampered with. Such a seal typically has a distinctive design \
 \ and other features that make it obvious when a sealed artifact \
 \ has been tampered with."
inherit SEAL
end

class_chart POLLING_STATION
explanation
  "A polling station is a location at which physical ballot \
 \ boxes and devices are available for members of the \
 \ electorate to use during an election. Polling stations \
 \ are typically staffed by poll workers and have well-publicized \
 \ hours of operation."
end

class_chart PRINCIPLE
explanation
  "A fundamental truth or proposition that serves as a foundation \
 \ for a system of belief or behavior or chain of reasoning."
end

class_chart PROCEDURE
explanation
  "An established or official way of doing something."
end

class_chart REFERENDUM
explanation
  "A referendum is a decision among two or more courses of \
 \ action. In most cases, referenda take the form of yes/no \
 \ questions."
end

class_chart REGIONAL
explanation
  "A regional scope is comprised of a large group of localities \
 \ or a single state, territory, or similar region. For example, \
 \ the state of California."
end

class_chart RELIABLE
explanation
  "An artifact is reliable if it is guaranteed to be \
 \ available for use, and to work properly, for a \
 \ specified percentage of time."
query
  "What is your reliability percentage?"
constraint
  "The reliability percentage must be between 0 and 100, \
 \ inclusive"
end

class_chart REMOTE
explanation
  "Located somewhere other than a polling station."
end

class_chart RESULT
explanation
  "The outcome of an election."
end

class_chart ROBUST
explanation
  "A process or system that is able to withstand adverse conditions."
end

class_chart SEAL
explanation 
  "A seal provides some evidence of the integrity of another \
 \ entity (such as a ballot or ballot box). Its form depends on \
 \ the form of the entity being sealed, as well as other system \
 \ characteristics."
query
  "Are you applied?",
  "What are you applied to?",
  "Are you broken?"
command
  "Apply yourself to the_entity!",
  "Break!"
constraint
  "A seal is initially not applied.",
  "A seal may only be applied if it has not been previously applied.",
  "A seal may only be broken if it has not been previously broken.",
  "Once applied, a seal remains applied forever.",
  "Once broken, a seal remains broken forever."
end

class_chart SECURITY
explanation
  "The attestation that a system is free of certain classes of \
 \ dangers or threats."
end

class_chart SYSTEM
explanation
  "A set of connected digital and physical parts, including computers, \
 \ people, organizations, and more, forming a complex whole."
end

class_chart TALLY
explanation
  "The count of all the votes in an election."
end

class_chart TURNOUT
explanation
  "The level of participation of voters in an election."
end

class_chart USABLE
explanation
  "An artifact is usable if it can successfully be used for its stated \
 \ purpose."
end

class_chart VERIFIABLE
explanation
  "A system or algorithm that has properties which can be \
 \ checked by people or digital systems."
end

class_chart VERIFICATION
explanation
  "A process by which the the election protocols, processes, and \
 \ device implementations are proven to be correct with respect to \
 \ their specifications."
end

class_chart VOTE
explanation
  "A vote is an expression of the choice of voting option made by \
 \ a voter during an election."
query
  "What choice do you represent?"
command
  "You represent the_choice!"
end

class_chart VOTER
explanation
  "A person who is eligible to vote in an election."
end

class_chart VOTER_REGISTRATION
explanation
  "The process by which a member of the electorate is added \
 \ to the voter register."
end

class_chart VOTING_CHANNEL
explanation
  "Methods by which voters are enabled to vote in elections."
end

class_chart VOTING_INTERFACE
explanation
  "The medium through which a voter makes his or \
 \ her choices of voting option known to the voting \
 \ system."
end

class_chart VOTING_OPTION
explanation
  "A selection from among the possible outcomes of an election."
end

class_chart VOTERS_REGISTER
explanation
  "A voters' register contains all the voters eligible \
 \ to vote in an election, as well as information \
 \ indicating whether each has actually voted."
query
  "Is the_voter in the register?",
  "Has the_voter voted?"
command
  "Add the_voter to the register!",
  "Remove the_voter from the register!",
  "the_voter has voted!"
constraint
  "A voter may not be added to the register more than once.",
  "A voter may not be removed from the register if he has voted.",
  "Once a voter is marked as having voted, he remains so marked forever."
end
